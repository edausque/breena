#!/usr/bin/perl
# could be helpful:
# http://poe.perl.org/?POE_Cookbook/IRC_Bots
# http://poe.perl.org/?POE_Cookbook/IRC_Bot_Debugging
# http://poe.perl.org/?POE_Cookbook/IRC_Bot_Disconnecting
# http://poe.perl.org/?POE_Cookbook/IRC_Bot_Reconnecting

use warnings;
use strict;
use IRC::Utils qw(parse_user lc_irc);
use POSIX qw(strftime);
use POE qw(Component::IRC Component::IRC::Plugin::Connector);
use POE::Component::IRC;
use Time::HiRes qw(time);
use Config::Simple;
use Data::Dumper;

my ($dirpath) = (__FILE__ =~ m{^(.*/)?.*}s);
if(!$dirpath) { $dirpath = "./"; }
my $conf_file = "${dirpath}breena.conf";

my $conf = new Config::Simple("$conf_file") or die "impossible de trouver $conf_file";
my $conf_nick = $conf->param("nick");
my $conf_nickserv = $conf->param("nickserv");
my $conf_server = $conf->param("server");
my $conf_channel = $conf->param("channel");
my $conf_debug = $conf->param("debug");

my @db_timers;

sub CHANNEL () { "$conf_channel" }

my ($irc) = POE::Component::IRC->spawn();

POE::Session->create(
  inline_states => {
    _start		=> \&bot_start,
    connect		=> \&bot_connect,
    irc_001		=> \&on_connect,
    irc_public		=> \&on_public,
    irc_ctcp_action	=> \&on_ctcp_action,
    irc_join            => \&on_join,
    irc_part            => \&on_part,
    irc_quit		=> \&on_quit,
    irc_disconnected	=> \&bot_reconnect,
    bot_timer		=> \&bot_timer,
  },
);

opendir my($dh), "${dirpath}Plugins" or die "Couldn't open dir '$dirpath': $!";
my @plugins_files = readdir $dh;
closedir $dh;
foreach my $plugin (@plugins_files)
{
  if($plugin =~ m/^([A-Za-z0-9]+)\.pm$/) {
    print "Load plugin: $1\n";
    my $module = "Plugins::$1";
    eval {
      (my $file = $module) =~ s|::|/|g;
      require $file . '.pm';
      my $loading_plugin = $module->new();
      $irc->plugin_add($module,$loading_plugin);
    };
    if ($@) {
      warn "Error including $module: $@";
    }
  }
}

sub bot_connect {
  my ($kernel, $heap) = @_[KERNEL ,HEAP];
  $irc->yield(register => "all");
  $heap->{connector} = POE::Component::IRC::Plugin::Connector->new();
  $irc->plugin_add( 'Connector' => $heap->{connector} );
  $irc->yield(
    connect => {
      Nick     => "$conf_nick",
      Username => "$conf_nick",
      Ircname  => 'POE::Component::IRC (Perl)',
      Server   => "$conf_server",
      Port     => '6667',
      debug    => "$conf_debug",
      plugin_debug    => "$conf_debug",
    }
  );
}

sub bot_start {
  my ($kernel, $heap) = @_[KERNEL, HEAP];
  $kernel->yield("connect");
}

sub on_connect {
  $irc->yield(privmsg => 'nickserv' => "identify $conf_nickserv");
  $irc->yield(join => CHANNEL);
  $irc->delay([ mode => $conf_channel => '-a' => $conf_nick ], 3);
  $irc->yield(privmsg => 'chanserv' => "op");
}

sub bot_reconnect {
  my ($kernel, $heap) = @_[KERNEL, HEAP];
  $kernel->delay(connect => 60);
}

sub on_ctcp_action {
  my ($sender, $who, $where, $what) = @_[SENDER, ARG0 .. ARG2];
  my $nick = ( split /!/, $who )[0];
  my $channel = $where->[0];
  if($what =~ /^(.*)$conf_nick(.*)$/) {
    $irc->yield(ctcp => $channel => "ACTION $1$nick$2");
  }
  return;
}

sub on_quit {
  my ($kernel, $who, $msg) = @_[KERNEL, ARG0, ARG1];
  my $nick    = (split /!/, $who)[0];
  if (($msg =~ /^Ping timeout$/) && ($nick eq 'Christina')) {
    $irc->yield(privmsg => "$conf_channel" => "hihihi");
  }
}

sub bot_timer {
  $irc->yield(privmsg => "$conf_channel" => "$_[ARG1]: ding!$_[ARG0]")
}

sub on_join {
  my $nick = parse_user($_[ARG0]);
  my $channel = $_[ARG1];
  if($nick ne $conf_nick) {
    $irc->yield(privmsg => $channel => "Hello $nick !");
  } else {
    $irc->yield(privmsg => $channel => "Hello !");
  }
}

sub on_part {
  my $nick = parse_user($_[ARG0]);
  my $channel = $_[ARG1];
  if($nick ne $conf_nick) {
    $irc->yield(privmsg => $channel => "Bye $nick !");
  }
}

sub on_public {
  my ($kernel, $who, $where, $msg) = @_[KERNEL, ARG0, ARG1, ARG2];
  my $nick    = (split /!/, $who)[0];
  my $channel = $where->[0];
  my $ts      = scalar localtime;

  if ($msg =~ /^t\s(\S*)\s*(.*?)$/) {
    my $seconds = $1;
    my $task = " $2";
    $seconds =~ s/[^mhd\d]//g; 
    $seconds =~ s/(\d+)(\w{1})(\d+)/$1$2+$3/g;
    $seconds =~ s/(\d+)(\w{1})(\d+)/$1$2+$3/g;
    $seconds =~ s/m/*60/g; 
    $seconds =~ s/h/*3600/g; 
    $seconds =~ s/d/*3600*24/g; 
    $seconds = eval($seconds);
    if($seconds =~ /^\d+$/) {
      $irc->yield(privmsg => $channel => "[timer added]$task ($seconds seconds)");
      $_[KERNEL]->delay_add(bot_timer => $seconds, $task, $nick);
    } else {
      $irc->yield(privmsg => $channel => "$nick, usage: t 1d2h42m10s [task]");
    }
  }
}

$poe_kernel->run();
exit 0;
